urllib = pyimport "urllib"
tf = pyimport "tarfile"
zf = pyimport "zipfile"
json = pyimport "json"
sys = pyimport "sys"
os = pyimport "os"
io = pyimport "io"
su = pyimport "shutil"
sub = pyimport "subprocess"

homedir = os.path.expanduser! "~"
erg_dir = homedir + "/.erg"
erg_bin_dir = homedir + "/.erg/bin"
erg_tmp_dir = homedir + "/.erg/tmp"

if! os.path.exists!(erg_dir), do!:
    print! ".erg directory already exists, do you want to overwrite it? [y/n]", end:=" "
    answer = input!()
    if! answer == "y":
        do!:
            print! "Removing \{erg_dir} ..."
            su.rmtree! erg_dir
        do!:
            print! "Aborting installation"
            exit 1

os.mkdir! erg_dir
os.mkdir! erg_bin_dir

latest_url = "https://api.github.com/repos/erg-lang/erg/releases/latest"
_stream = urllib.request.urlopen!(latest_url)
s = _stream.read!().decode()
jdata = json.loads s
assert jdata in {Str: Obj}
latest_version = jdata["tag_name"]

print! "version: \{latest_version}"

filename = match sys.platform:
    "darwin" -> "erg-x86_64-apple-darwin.tar.gz"
    "win32" -> "erg-x86_64-pc-windows-msvc.zip"
    _ -> "erg-x86_64-unknown-linux-gnu.tar.gz"
url = "https://github.com/erg-lang/erg/releases/download/\{latest_version}/\{filename}"

print! "Downloading \{url} ..."

stream = urllib.request.urlopen!(url)
if! sys.platform == "win32":
    do!:
        print! "Extracting \{filename} ..."
        bytesio = io.BytesIO! stream.read!()
        zipfile = zf.ZipFile! bytesio
        zipfile.extractall! erg_tmp_dir
        zipfile.close!()
        discard su.move! "\{erg_tmp_dir}/erg.exe", "\{erg_bin_dir}/erg.exe"
        discard su.move! "\{erg_tmp_dir}/lib", "\{erg_dir}/lib"
        su.rmtree! erg_tmp_dir
    do!:
        print! "Extracting \{filename} ..."
        tarfile = tf.open!(fileobj:=stream, mode:="r|gz")
        tarfile.extractall! erg_tmp_dir
        tarfile.close!()
        discard su.move! "\{erg_tmp_dir}/erg", "\{erg_bin_dir}/erg"
        discard su.move! "\{erg_tmp_dir}/lib", "\{erg_dir}/lib"
        su.rmtree! erg_tmp_dir

print! "erg installed successfully"

poise_git_url = "https://github.com/erg-lang/poise.git"
print! "Cloning poise (erg package manager) ..."
if! not(os.path.exists!(erg_tmp_dir)), do!:
    os.mkdir! erg_tmp_dir
os.chdir! erg_tmp_dir
res = sub.run! ["git", "clone", poise_git_url], capture_output:=True
if! res.returncode != 0, do!:
    panic "Failed to clone poise repo"
os.chdir! "poise"
print! "Building poise ..."
res2 = sub.run! ["erg", "src/main.er", "--", "install"], capture_output:=True
if! res2.returncode != 0, do!:
    assert res2.stderr in Bytes
    panic "Failed to install poise: \{res2.stderr.decode()}"
print! "poise installed successfully"
os.chdir! ".."
su.rmtree! "poise"

print! "Please add `.erg` to your PATH by running `export PATH=$PATH:\{erg_bin_dir}` and `export ERG_PATH=\{erg_dir}`"
