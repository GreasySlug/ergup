urllib = pyimport "urllib"
tf = pyimport "tarfile"
zf = pyimport "zipfile"
json = pyimport "json"
sys = pyimport "sys"
os = pyimport "os"
io = pyimport "io"
su = pyimport "shutil"

latest_url = "https://api.github.com/repos/erg-lang/erg/releases/latest"
_stream = urllib.request.urlopen!(latest_url)
s = _stream.read!().decode()
jdata = json.loads s
assert jdata in {Str: Str}
latest_version = jdata["tag_name"]

print! "version: " + latest_version

filename = match sys.platform:
    "darwin" -> "erg-x86_64-apple-darwin.tar.gz"
    "win32" -> "erg-x86_64-pc-windows-msvc.zip"
    _ -> "erg-x86_64-unknown-linux-gnu.tar.gz"
url = "https://github.com/erg-lang/erg/releases/download/" + latest_version + "/" + filename

print! "downloading " + url + " ..."

homedir = os.path.expanduser! "~"
os.mkdir! homedir + "/.erg"
os.mkdir! homedir + "/.erg/bin"

stream = urllib.request.urlopen!(url)
if! sys.platform == "win32":
    do!:
        print! "extracting " + filename + " ..."
        bytesio = io.newBytesIO stream.read!()
        zipfile = zf.open! bytesio
        zipfile.extractall!()
        zipfile.close!()
        su.move! "erg.exe", homedir + "/.erg/bin/erg.exe"
        su.move! "lib", homedir + "/.erg/lib"
    do!:
        print! "extracting " + filename + " ..."
        tarfile = tf.open!(fileobj:=stream, mode:="r|gz")
        tarfile.extractall!()
        tarfile.close!()
        su.move! "erg", homedir + "/.erg/bin/erg"
        su.move! "lib", homedir + "/.erg/lib"

print! "please add `.erg` to your PATH by running `export PATH=$PATH:" + homedir + "/.erg/bin` and `export ERG_PATH=" + homedir + "/.erg`"
